plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

group = 'fr.mime'
version = '1.1.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = 'PlugMan'
        url = 'https://raw.githubusercontent.com/TheBlackEntity/PlugMan/repository/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.rylinaux:PlugMan:2.3.3'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'com.github.zafarkhaja:java-semver:0.10.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "MimeLib-${version}.jar"
    relocate 'org.bstats', 'fr.mime.bstats'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'fr.mime'
            artifactId = 'MimeLib'
            version = '1.1.1'

            from components.java
        }
        mavenJava(MavenPublication) {
            pom {
                name = 'MimeLib'
                url = 'https://discord.mimedev.fr'
            }
        }
    }
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

